plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'org.waraccademy'
version = '1.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name= 'jitpack.io'
        url = 'https://jitpack.io'
    }
    maven {
        name="nbtapi"
        url = "https://repo.codemc.org/repository/maven-public/"
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'com.arcaniax:HeadDatabase-API:1.3.1'
    compileOnly 'org.waraccademy:RoleplayItems:1.0.0-SNAPSHOT'
    compileOnly 'org.waraccademy:Containers:1.0.1-SNAPSHOT'
    compileOnly 'org.waraccademy:MongoConnector:1.0.0-SNAPSHOT'

    implementation 'de.tr7zw:item-nbt-api:2.10.0'
    implementation 'com.github.glyart:mystral:1.4.4-SNAPSHOT'
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar{
    relocate 'com.glyart.mystral' , 'org.waraccademy.posta.mystral'
    relocate 'com.zaxxer.hikari' , 'org.waraccademy.posta.hikari'
    relocate 'de.tr7zw.changeme.nbtapi' , 'org.waraccademy.posta.nbtapi'
}
